import de.undercouch.gradle.tasks.download.Download

apply plugin: "com.liferay.test.integration"
apply plugin: "de.undercouch.download"

task copyBundleBinDir(type: Copy)
task downloadBundle(type: Download)
task runGradleTest

buildscript{
	dependencies {
		classpath group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"
	}
	
	repositories {
		mavenLocal()

		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

File binDir = new File(projectDir, "bin")

String bundleUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"
}

copyBundleBinDir {
	dependsOn downloadBundle

	eachFile { file ->
		file.path = file.path.replaceFirst('**/bin', '')
	}

	from {
		zipTree(downloadBundle.dest)
        include '**/bin/'
	}

	includeEmptyDirs = false
	into binDir
}

copyTestModules {
	enabled = false
}

downloadBundle {
	dest new File(buildDir, "bundle.zip")
	src bundleUrl
}

runGradleTest {
	dependsOn copyBundleBinDir
	dependsOn setUpTestableTomcat

	doLast {
		for (String fileName in ["catalina.sh", "setenv.sh", "shutdown.sh"]) {
			File shFile = new File(binDir, fileName)

			assert shFile.canExecute()
		}
	}
}

testIntegrationTomcat {
	dir = binDir
	liferayHome = projectDir
}