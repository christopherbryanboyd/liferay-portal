import com.bmuschko.gradle.docker.tasks.container.DockerLogsContainer

import java.net.InetAddress

buildscript {
	dependencies {
		classpath fileTree(dir: pluginClasspathDir, excludes: ["biz.aQute.bnd-3.5.0.jar", "com.liferay.gradle.util-1.0.33.jar"], include: "*.jar")
	}
}

apply plugin: "com.liferay.workspace"

String expectedLine = "Hello World"

gradle.liferayWorkspace {
	dockerImageId = InetAddress.localHost.canonicalHostName.toLowerCase() + 'startdockercontainerwithscript'
	dockerContainerId = InetAddress.localHost.canonicalHostName.toLowerCase() + 'startdockercontainerwithscript'
}

gradle.projectsEvaluated {
	Project root = gradle.rootProject
	root.tasks.register("assertLog", DockerLogsContainer) {
		dependsOn root.tasks.startDockerContainer
		finalizedBy root.tasks.cleanDockerImage
		tailAll = true
		follow = false
		targetContainerId {
			gradle.liferayWorkspace.dockerContainerId
		}

		boolean expectedLineDetected = false

		sink new PrintWriter(new StringWriter()) {
			public void write(String line) {
				if (line.contains(expectedLine)) {
					expectedLineDetected = true
				}
				root.print line
			}
		}

		doLast {
			int retries = 0

			while (!expectedLineDetected && retries < 50) {
				Thread.sleep(100)
				retries++
				start()
			}

			if (!expectedLineDetected) {
				throw new GradleException("The expected line " + expectedLine + " was not found in the log.")
			}
		}
	}
}